buildscript {
	ext.mainRootDir = rootProject.projectDir.absolutePath

	apply from: file("project_config.gradle")

	repositories {
		google()
		jcenter()
		maven {
			name = "r8"
			url = "https://storage.googleapis.com/r8-releases/raw"
			content { includeGroup("com.android.tools") }
		}
	}

	dependencies {
		classpath "com.android.tools:r8:3.1.64"
		classpath "com.android.tools.build:gradle:7.1.0"
	}
}

subprojects {
	repositories {
		google()
		jcenter()
	}

	def javaPlugin = "org.gradle.api.plugins.JavaPlugin"
	def javaBasePlugin = "org.gradle.api.plugins.JavaBasePlugin"
	def javaBasePluginInject = javaBasePlugin + '$' + "Inject"
	def androidLibraryPlugin = "com.android.build.gradle.LibraryPlugin"
	def androidAppPlugin = "com.android.build.gradle.AppPlugin"
	project.plugins.whenPluginAdded { plugin ->
		switch (plugin.class.name) {
			case [javaPlugin, javaBasePlugin, javaBasePluginInject]:
				project.apply from: file("$mainRootDir/gradle/plugin_java.gradle")
				break
			case androidLibraryPlugin:
				project.apply from: file("$mainRootDir/gradle/plugin_android_library.gradle")
				break
			case androidAppPlugin:
				project.apply from: file("$mainRootDir/gradle/plugin_android_app.gradle")
				break
		}
	}
}

if (hasProperty("buildScan")) {
	buildScan {
		termsOfServiceUrl = "https://gradle.com/terms-of-service"
		termsOfServiceAgree = "yes"
	}
}

wrapper {
	distributionType = Wrapper.DistributionType.BIN
}

apply from: file("gradle/tasks.gradle")